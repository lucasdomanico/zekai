# lambda or anonymous functions work like js/ts
# but with a different syntax

f
    v [1 2 3]
    q v.map(\x: x * 2) # inline lambda expression
    q.map(\x\ x * 2) # you can also use another \ instead of :

sad
    \: 0 # avoid sad emojis when there are no arguments!
    \\ 0 # use double \

# you can also use lambda expressions with a code block/suite as the body
suite
    v [1 2 3]
    v.map(\x
        n 10
        n *= 20
        if false # you can use statements like expected
            ret 0
        for
            break
        x * n
    )

# to use an async lambda expression, just use the async keyword before the arguments
wait
    v [1 2 3]
    v.forEach(async \x
        console.log('one')
        await timeout()
        console.log('two')
    )

Promise(void)
timeout
    Promise(\resolve
        setTimeout(resolve 1000)
    )

# note that defuns are a special syntax for functions
# you can write a defun like
num num num
add a b: a + b
# as a global with a lambada
@fun(num num num)
sub \a b: a - b

main
    console.log(f())
    console.log(sad())
    console.log(suite())
    wait()
    console.log(add(1 2))
    console.log(sub(1 2))